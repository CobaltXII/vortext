<html>
	<head>
		<title>Vortext</title>
		<style>
body {
	margin: 0px;
}
canvas {
	width: 100%;
	height: 100%;
}
		</style>
	</head>
	<body>
		<script id="vsSource" type="x-shader/x-vertex">
precision mediump float;

attribute vec3 aVertexPosition;

void main() {
	gl_Position = vec4(aVertexPosition, 1.0);
}
		</script>
		<script id="fsSource" type="x-shader/x-fragment">
precision mediump float;

uniform vec2 iResolution;
uniform float iTime;
uniform float iOffsetZ;
uniform vec3 iColor0;
uniform vec3 iColor1;

#define THICKNESS 0.5

// **************************************************************** //
// **************************************************************** //
// **** PRIMITIVES                                             **** //
// **************************************************************** //
// **************************************************************** //

// Constant torus definition.
const vec2 torus = vec2(2.0, THICKNESS);

// SDF of a torus.
float sdf_torus(in vec3 pos, in vec3 offset) {
    pos -= offset;
    vec2 q = vec2(length(pos.xy) - torus.x, pos.z);
    return length(q) - torus.y;
}

// SDF of the top half of a torus.
float sdf_half0_torus(in vec3 pos, in vec3 offset) {
    pos -= offset;
    vec2 q = vec2(length(pos.xy) - torus.x, pos.z);
    return max(length(q) - torus.y, -pos.y);
}

// SDF of the bottom half of a torus.
float sdf_half1_torus(in vec3 pos, in vec3 offset) {
    pos -= offset;
    vec2 q = vec2(length(pos.xy) - torus.x, pos.z);
    return max(length(q) - torus.y, pos.y);
}

// SDF of the left half of a torus.
float sdf_half2_torus(in vec3 pos, in vec3 offset) {
    pos -= offset;
    vec2 q = vec2(length(pos.xy) - torus.x, pos.z);
    return max(length(q) - torus.y, pos.x);
}

// SDF of the right half of a torus.
float sdf_half3_torus(in vec3 pos, in vec3 offset) {
    pos -= offset;
    vec2 q = vec2(length(pos.xy) - torus.x, pos.z);
    return max(length(q) - torus.y, -pos.x);
}

// SDF of the top-left quarter of a torus.
float sdf_quarter0_torus(in vec3 pos, in vec3 offset) {
    pos -= offset;
    vec2 q = vec2(length(pos.xy) - torus.x, pos.z);
    return max(length(q) - torus.y, max(-pos.y, pos.x));
}

// SDF of the top-right quarter of a torus.
float sdf_quarter1_torus(in vec3 pos, in vec3 offset) {
    pos -= offset;
    vec2 q = vec2(length(pos.xy) - torus.x, pos.z);
    return max(length(q) - torus.y, max(-pos.y, -pos.x));
}

// SDF of the bottom-left quarter of a torus.
float sdf_quarter2_torus(in vec3 pos, in vec3 offset) {
    pos -= offset;
    vec2 q = vec2(length(pos.xy) - torus.x, pos.z);
    return max(length(q) - torus.y, max(pos.y, pos.x));
}

// SDF of the bottom-right quarter of a torus.
float sdf_quarter3_torus(in vec3 pos, in vec3 offset) {
    pos -= offset;
    vec2 q = vec2(length(pos.xy) - torus.x, pos.z);
    return max(length(q) - torus.y, max(pos.y, -pos.x));
}

// SDF of a capsule.
float sdf_capsule(in vec3 pos, in vec3 a, in vec3 b) {
    vec3 pa = pos - a, ba = b - a;
    float h = clamp(dot(pa, ba) / dot(ba , ba), 0.0, 1.0);
    return length(pa - ba * h) - THICKNESS;
}

// SDF of a sphere.
float sdf_sphere(in vec3 pos, in vec3 offset) {
    return length(pos - offset) - THICKNESS;
}

// **************************************************************** //
// **************************************************************** //
// **** LOWERCASE                                              **** //
// **************************************************************** //
// **************************************************************** //

// SDF for the letter A.
float sdf_a(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(0, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter B.
float sdf_b(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -4, 0), vec3(4, -6, 0)));
    return t;
}

// SDF for the letter C.
float sdf_c(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(0, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -4, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -6, 0)));
    return t;
}

// SDF for the letter D.
float sdf_d(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(0, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter E.
float sdf_e(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(0, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4.5, 0), vec3(4, -4.5, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -4.5, 0), vec3(4, -4, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -6, 0)));
    return t;
}

// SDF for the letter F.
float sdf_f(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(2, -2, 0), vec3(2, -8, 0)));
    t = min(t, sdf_quarter0_torus(pos, vec3(4, -2, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4.5, 0), vec3(4, -4.5, 0)));
    return t;
}

// SDF for the letter G.
float sdf_g(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -6, 0)));
    t = min(t, sdf_quarter3_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(2, -8, 0)));
    return t;
}

// SDF for the letter H.
float sdf_h(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -4, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter I.
float sdf_i(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -8, 0)));
    t = min(t, sdf_sphere(pos, vec3(0, -0.5, 0)));
    return t;
}

// SDF for the letter J.
float sdf_j(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -0.5, 0)));
    t = min(t, sdf_sphere(pos, vec3(0, -6, 0)));
    return t;
}

// SDF for the letter K.
float sdf_k(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -6, 0), vec3(4, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -6, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter L.
float sdf_l(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -8, 0)));
    return t;
}

// SDF for the letter M.
float sdf_m(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -8, 0)));
    t = min(t, sdf_half0_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_half0_torus(pos, vec3(6, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -4, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(8, -4, 0), vec3(8, -8, 0)));
    
    return t;
}

// SDF for the letter N.
float sdf_n(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -4, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter O.
float sdf_o(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(0, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -4, 0), vec3(4, -6, 0)));
    return t;
}

// SDF for the letter P.
float sdf_p(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(0, -8, 0)));
    return t;
}

// SDF for the letter Q.
float sdf_q(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -4, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter R.
float sdf_r(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -8, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -4, 0)));
    return t;
}

// SDF for the letter S.
float sdf_s(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(4, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(0, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -4, 0)));
    return t;
}

// SDF for the letter T.
float sdf_t(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(2, 0, 0), vec3(2, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(4, -2, 0)));
    return t;
}

// SDF for the letter U.
float sdf_u(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -6, 0)));
    return t;
}

// SDF for the letter V.
float sdf_v(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(2, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(2, -8, 0)));
    return t;
}

// SDF for the letter W.
float sdf_w(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(2, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(2, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(6, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(8, -2, 0), vec3(6, -8, 0)));
    return t;
}

// SDF for the letter X.
float sdf_x(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(0, -8, 0)));
    return t;
}

// SDF for the letter Y.
float sdf_y(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half1_torus(pos, vec3(2, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -4, 0)));
    t = min(t, sdf_quarter3_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(2, -8, 0)));
    return t;
}

// SDF for the letter Z.
float sdf_z(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(4, -2, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(0, -8, 0)));
    return t;
}

// **************************************************************** //
// **************************************************************** //
// **** UPPERCASE                                              **** //
// **************************************************************** //
// **************************************************************** //

// SDF for the letter A.
float sdf_A(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(4, -4, 0)));
    return t;
}

// SDF for the letter B.
float sdf_B(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(2, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(2, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(2, -8, 0)));
    t = min(t, sdf_half3_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_half3_torus(pos, vec3(2, -6, 0)));
    return t;
}

// SDF for the letter C.
float sdf_C(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -2, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -6, 0)));
    return t;
}

// SDF for the letter D.
float sdf_D(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -6, 0)));
    t = min(t, sdf_quarter1_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_quarter3_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(2, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(2, -8, 0)));
    return t;
}

// SDF for the letter E.
float sdf_E(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_quarter0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_quarter2_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(4, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(2, 0, 0), vec3(4, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(2, -8, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter F.
float sdf_F(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_quarter0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(4, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(2, 0, 0), vec3(4, 0, 0)));
    return t;
}

// SDF for the letter G.
float sdf_G(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -2, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -4, 0), vec3(4, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(2, -4, 0), vec3(4, -4, 0)));
    return t;
}

// SDF for the letter H.
float sdf_H(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(4, -4, 0)));
    return t;
}

// SDF for the letter I.
float sdf_I(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(4, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(2, 0, 0), vec3(2, -8, 0)));
    return t;
}

// SDF for the letter J.
float sdf_J(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(4, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(4, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(0, -6, 0)));
    return t;
}

// SDF for the letter K.
float sdf_K(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(2, -4, 0)));
    t = min(t, sdf_quarter1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_quarter3_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(4, -2, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -6, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter L.
float sdf_L(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_quarter2_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(0, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(2, -8, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter M.
float sdf_M(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_half0_torus(pos, vec3(6, -2, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(8, -2, 0), vec3(8, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -8, 0)));
    return t;
}

// SDF for the letter N.
float sdf_N(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter O.
float sdf_O(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -6, 0)));
    return t;
}

// SDF for the letter P.
float sdf_P(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(2, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(2, -4, 0)));
    t = min(t, sdf_half3_torus(pos, vec3(2, -2, 0)));
    return t;
}

// SDF for the letter Q.
float sdf_Q(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(2, -6, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter R.
float sdf_R(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(2, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(2, -4, 0)));
    t = min(t, sdf_half3_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_quarter1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -6, 0), vec3(4, -8, 0)));
    return t;
}

// SDF for the letter S.
float sdf_S(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_quarter2_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_quarter1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(0, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -2, 0)));
    return t;
}

// SDF for the letter T.
float sdf_T(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(4, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(2, 0, 0), vec3(2, -8, 0)));
    return t;
}

// SDF for the letter U.
float sdf_U(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(4, -6, 0)));
    return t;
}

// SDF for the letter V.
float sdf_V(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(2, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(2, -8, 0)));
    return t;
}

// SDF for the letter W.
float sdf_W(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(6, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(4, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(8, 0, 0), vec3(8, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -6, 0)));
    return t;
}

// SDF for the letter X.
float sdf_X(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(0, -8, 0)));
    return t;
}

// SDF for the letter Y.
float sdf_Y(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -2, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(4, -2, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_capsule(pos, vec3(2, -4, 0), vec3(2, -8, 0)));
    return t;
}

// SDF for the letter Z.
float sdf_Z(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(4, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(0, -8, 0)));
    return t;
}

// **************************************************************** //
// **************************************************************** //
// **** NUMBERS                                                **** //
// **************************************************************** //
// **************************************************************** //

// SDF for the number 0.
float sdf_0(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(0, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -2, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -6, 0)));
    return t;
}

// SDF for the number 1.
float sdf_1(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(2, 0, 0), vec3(2, -8, 0)));
    t = min(t, sdf_quarter0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_sphere(pos, vec3(0, -2, 0)));
    return t;
}

// SDF for the number 2.
float sdf_2(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(4, -2, 0)));
    t = min(t, sdf_half0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_sphere(pos, vec3(0, -2, 0)));
    return t;
}

// SDF for the number 3.
float sdf_3(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(2, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(2, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(2, -8, 0)));
    t = min(t, sdf_half3_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_half3_torus(pos, vec3(2, -6, 0)));
    return t;
}

// SDF for the number 4.
float sdf_4(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(4, 0, 0), vec3(4, -8, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -2, 0)));
    t = min(t, sdf_capsule(pos, vec3(2, -4, 0), vec3(4, -4, 0)));
    t = min(t, sdf_quarter2_torus(pos, vec3(2, -2, 0)));
    return t;
}

// SDF for the number 5.
float sdf_5(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(4, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(0, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -4, 0), vec3(2, -4, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(2, -8, 0)));
    t = min(t, sdf_half3_torus(pos, vec3(2, -6, 0)));
    return t;
}

// SDF for the number 6.
float sdf_6(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_half0_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_half1_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_half0_torus(pos, vec3(2, -6, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, -2, 0), vec3(0, -6, 0)));
    t = min(t, sdf_sphere(pos, vec3(4, -2, 0)));
    return t;
}

// SDF for the number 7.
float sdf_7(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_capsule(pos, vec3(0, -8, 0), vec3(4, 0, 0)));
    t = min(t, sdf_capsule(pos, vec3(0, 0, 0), vec3(4, 0, 0)));
    return t;
}

// SDF for the number 8.
float sdf_8(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_torus(pos, vec3(2, -2, 0)));
    t = min(t, sdf_torus(pos, vec3(2, -6, 0)));
    return t;
}

// SDF for the number 9.
float sdf_9(in vec3 pos, in vec3 offset) {
    pos -= offset;
    float t = 1000.0;
    t = min(t, sdf_torus(pos, vec3(2, -2, 0)));
	t = min(t, sdf_capsule(pos, vec3(4, -2, 0), vec3(4, -8, 0)));
    return t;
}

// **************************************************************** //
// **************************************************************** //
// **** SCENE                                                  **** //
// **************************************************************** //
// **************************************************************** //

$$$SDF$$$

// Calculate the surface normal of the scene at a certain point.
// http://iquilezles.org/www/articles/normalsSDF/normalsSDF.htm
vec3 calc_normal(in vec3 pos) {
    const float ep = 0.0001;
    vec2 e = vec2(1.0, -1.0) * 0.5773;
    return normalize(e.xyy * sdf(pos + e.xyy * ep) + 
                     e.yyx * sdf(pos + e.yyx * ep) + 
                     e.yxy * sdf(pos + e.yxy * ep) + 
                     e.xxx * sdf(pos + e.xxx * ep));
}

// Calculate soft shadows.
// http://iquilezles.org/www/articles/rmshadows/rmshadows.htm
float calc_soft_shadows(in vec3 ro, in vec3 rd, in float tmin, in float tmax, const float k) {
    float res = 1.0;
    float t = tmin;
    for (int i = 0; i < 50; i++) {
        float h = sdf(ro + rd * t);
        res = min(res, k * h / t);
        t += clamp(h, 0.02, 0.20);
        if (res < 0.005 || t > tmax) {
            break;
        }
    }
    return clamp(res, 0.0, 1.0);
}

// Gradient noise function.
// https://www.shadertoy.com/view/Xsl3Dl
vec3 hash(in vec3 p) {
	p = vec3(dot(p, vec3(127.1, 311.7, 074.7)),
			 dot(p, vec3(269.5, 183.3, 246.1)),
			 dot(p, vec3(113.5, 271.9, 124.6)));

	return -1.0 + 2.0 * fract(sin(p) * 43758.5453123);
}

// Gradient noise function.
// https://www.shadertoy.com/view/Xsl3Dl
float noise(in vec3 p) {
    vec3 i = floor(p);
    vec3 f = fract(p);
	
	vec3 u = f * f * (3.0 - 2.0 * f);

    return mix(mix(mix(dot(hash(i + vec3(0.0, 0.0, 0.0)), f - vec3(0.0, 0.0, 0.0)), 
                       dot(hash(i + vec3(1.0, 0.0, 0.0)), f - vec3(1.0, 0.0, 0.0)), u.x),
                   mix(dot(hash(i + vec3(0.0, 1.0, 0.0)), f - vec3(0.0, 1.0, 0.0)), 
                       dot(hash(i + vec3(1.0, 1.0, 0.0)), f - vec3(1.0, 1.0, 0.0)), u.x), u.y),
               mix(mix(dot(hash(i + vec3(0.0, 0.0, 1.0)), f - vec3(0.0, 0.0, 1.0)), 
                       dot(hash(i + vec3(1.0, 0.0, 1.0)), f - vec3(1.0, 0.0, 1.0)), u.x),
                   mix(dot(hash(i + vec3(0.0, 1.0, 1.0)), f - vec3(0.0, 1.0, 1.0)), 
                       dot(hash(i + vec3(1.0, 1.0, 1.0)), f - vec3(1.0, 1.0, 1.0)), u.x), u.y), u.z);
}

// Rotation matrices.
mat3 x_rotate(float a) {
    return mat3(1.0, 0.0, 0.0,
                0.0, cos(a), -sin(a),
                0.0, sin(a), cos(a));
}
mat3 y_rotate(float a) {
    return mat3(cos(a), 0.0, sin(a),
                0.0, 1.0, 0.0,
                -sin(a), 0.0, cos(0.0));
}
mat3 z_rotate(float a) {
    return mat3(cos(a), -sin(a), 0.0,
                sin(a), cos(a), 0.0,
                0.0, 0.0, 1.0);
}

// Anti-aliasing level.
#define AA 1

// Main shader.
void main() {
    vec3 lig = normalize(vec3(0.0, 4.0, 1.0));
    
    vec3 tot = vec3(0.0);
    
    // Do multiple samples for anti-aliasing.
    for (int m = 0; m < AA; m++)
    for (int n = 0; n < AA; n++) {
        // Offset pixel coordinate.
        vec2 o = vec2(float(m), float(n)) / float(AA) - 0.5;
        vec2 p = (gl_FragCoord.xy / iResolution * 2.0 - 1.0);
        p.x *= iResolution.x / iResolution.y;
 
        // Calculate ray parameters.
        vec3 ro = vec3(p * (z_offset + iOffsetZ) * 0.75, 0.0);
        vec3 rd = vec3(0.0, 0.0, -1.0);

       	// Maximum ray distance.
        const float tmax = 10000.0;
        
        // Cast ray.
        float t = 0.0;
        for (int i = 0; i < 64; i++) {
            vec3 p = ro + t * rd;
            float h = sdf(p);
            if (abs(h) < 0.001) {
                break;
            }
            t += h;
        }

        // Calculate lighting if the ray hit something.
        vec3 col = vec3(0.0);
        if (t < tmax)
        {
            vec3 pos = ro + t * rd;
            vec3 nor = calc_normal(pos); 
            float dif = clamp(dot(nor, lig), 0.0, 1.0);
            pos.y -= iTime;
            pos.z += iTime;
            float n = 1.0000 * noise(pos * 1.0) +
                      0.5000 * noise(pos * 2.0) +
                      0.2500 * noise(pos * 4.0) +
                      0.1250 * noise(pos * 8.0) +
                      0.0650 * noise(pos * 16.0);
            n /= 0.5;
            col = clamp(mix(iColor0, iColor1, gl_FragCoord.x / iResolution.x) + vec3(n), vec3(0.0), vec3(1.0)) + dif;
        } else {
            vec3 pos = ro / z_offset + 1.0;
            pos.z += iTime / 10.0;
            float n = 1.0000 * noise(pos * 1.0) +
                      0.5000 * noise(pos * 2.0) +
                      0.2500 * noise(pos * 4.0) +
                      0.1250 * noise(pos * 8.0) +
                      0.0650 * noise(pos * 16.0);
            col = vec3(pow((n + 1.0) / 2.0, 8.0));
        }
        
        tot += sqrt(col);
    }
    
    tot /= float(AA * AA);
    
    const float temp = -20.0 / 255.0;
    tot.x += temp;
    tot.z -= temp;
    gl_FragColor = vec4(tot, 1.0);
}
		</script>
		<canvas id="glCanvas"></canvas>
		<script type="text/javascript" src="dat.gui.min.js"></script>
		<script type="text/javascript" src="main.js"></script>
	</body>
</html>